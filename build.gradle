plugins {
    id 'fabric-loom' version '0.7-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
group = project.maven_group

if (System.env.CI == "true") {
    version = "${project.mod_version}+a${System.env.GITHUB_RUN_NUMBER}.${System.env.GITHUB_REF.replaceAll("refs/heads/", "").replaceAll("/", "-")}"
}
else {
    version = "${project.mod_version}+${project.minecraft_version}-fabric"
}

minecraft {}

repositories {
    maven { url "https://jitpack.io/" }
    maven { url "https://maven.dblsaiko.net/" }
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven { url "https://server.bbkr.space/artifactory/libs-release/" }
    maven { url 'https://maven.blamejared.com/' }
    maven { url "https://maven.fabricmc.net/" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.shadew.net/" }
    maven { url "https://maven.enginehub.org/repo/" }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_build}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

    /*
     * COMPATIBILITY
     */

    String canvas = "grondag:canvas-mc116:${project.canvas_version}"
    if (!Boolean.parseBoolean("${project.ip_enabled}") && Boolean.parseBoolean("${project.canvas_enabled}")) { // Immersive Portals is incompatible with Canvas
        modImplementation(canvas) {
            exclude group: "net.fabricmc.fabric-api"
            exclude group: "me.shedaniel.cloth"
            exclude group: "io.github.prospector"
        }
    } else {
        modCompileOnly(canvas) {
            exclude group: "net.fabricmc.fabric-api"
            exclude group: "me.shedaniel.cloth"
            exclude group: "io.github.prospector"
        }
    }

    String ip = "com.github.qouteall.ImmersivePortalsMod:build:${project.ip_version}"
    if (Boolean.parseBoolean("${project.ip_enabled}")) {
        modImplementation(ip) {
            exclude group: "net.fabricmc.fabric-api"
            exclude group: "me.shedaniel.cloth"
            exclude group: "com.github.Virtuoel"
            exclude group: "io.github.prospector"
        }
    } else {
        modCompileOnly(ip) {
            exclude group: "net.fabricmc.fabric-api"
            exclude group: "me.shedaniel.cloth"
            exclude group: "com.github.Virtuoel"
            exclude group: "io.github.prospector"
        }
    }

    modImplementation("com.terraformersmc:modmenu:${project.mod_menu_version}") { exclude group: "net.fabricmc.fabric-api" }
    modImplementation("com.github.teamhollow:fallingblocktweaks:${project.fbt_version}") { exclude group: "net.fabricmc.fabric-api" }

    /*
     * LIBRARIES
     */
    include("com.github.andantedevs:chord:${project.chord_version}") { exclude group: "net.fabricmc.fabric-api" }
    modImplementation("com.github.andantedevs:chord:${project.chord_version}") { exclude group: "net.fabricmc.fabric-api" }

    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"
    include "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"

    modImplementation("net.shadew:jedt:${project.jedt_version}") { exclude group: "net.fabricmc.fabric-api" }

    include("net.shadew:shwutil:${project.shwutil_version}")
    implementation("net.shadew:shwutil:${project.shwutil_version}") // not a mod, no need for modImplementation

    include("net.shadew:ptg:${project.ptg_version}")
    implementation("net.shadew:ptg:${project.ptg_version}") // neither a mod

    /*
     * RUNTIME UTILITIES
     */

    modRuntime("com.github.joaoh1:OkZoomer:${project.okzoomer_version}") {
        exclude group: "net.fabricmc.fabric-api"
        exclude group: "me.shedaniel.cloth"
        exclude group: "io.github.prospector"
    }

    String sodium = "com.github.CaffeineMC:sodium-fabric:${project.sodium_version}"
    if (!Boolean.parseBoolean("${project.ip_enabled}") && !Boolean.parseBoolean("${project.canvas_enabled}")) {
        modRuntime(sodium) { exclude group: "net.fabricmc.fabric-api" }
    } else {
        modCompileOnly(sodium) { exclude group: "net.fabricmc.fabric-api" }
    }
    // modRuntime("com.github.CaffeineMC:hydrogen-fabric:${project.hydrogen_version}") { exclude group: "net.fabricmc.fabric-api" }
    modRuntime("com.github.CaffeineMC:lithium-fabric:${project.lithium_version}") { exclude group: "net.fabricmc.fabric-api" }
    modRuntime("com.github.CaffeineMC:phosphor-fabric:${project.phosphor_version}") { exclude group: "net.fabricmc.fabric-api" }
}

loom {
    accessWidener "src/main/resources/${project.archives_base_name}.accesswidener"

    runs {
        data {
            server()

            vmArg "-Dmarbles.datagen=true"
            vmArg "-Dmarbles.datagen.path=${project.file("src/generated/resources")};${project.file("$buildDir/resources/main")}"
        }
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    def targetVersion = 8
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
        }
    }
}

processResources {
    // Specify auto_datagen in your local gradle properties (~/.gradle/gradle.properties) if you want datagen to run
    // automatically
    if (project.getProperties().containsKey("auto_datagen") && Boolean.parseBoolean("${project.getProperties().get("auto_datagen")}")) {
        finalizedBy runData
    }
}

sourceSets.main.resources {
    srcDirs += "$projectDir/src/generated/resources"
}

loom {
    accessWidener "src/main/resources/marbles.accesswidener"
}
